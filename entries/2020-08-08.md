# LLVM Dominance

In LLVM, it's a little unclear what values are considered in-scope in any
given block. It has to do with *dominance*. In a discussion of
[well-formedness](https://llvm.org/docs/LangRef.html#well-formedness) in the
LLVM manual, we see that: 

> It is important to note that this document describes 'well formed' LLVM
> assembly language. There is a difference between what the parser accepts
> and what is considered 'well formed'. For example, the following
> instruction is syntactically okay, but not well formed:
>
>     %x = add i32 1, %x
>
> because the definition of `%x` does not dominate all of its uses. The LLVM
> infrastructure provides a verification pass that may be used to verify that
> an LLVM module is well formed. This pass is automatically run by the parser
> after parsing input assembly and by the optimizer before it outputs bitcode.
> The violations pointed out by the verifier pass indicate bugs in
> transformation passes or input to the parser.

Alright, but what exactly is dominance? Wikipedia actually offers
[a pretty good description](https://en.wikipedia.org/wiki/Dominator_(graph_theory):

> ... a node `d` dominates a node `n` if every path from the entry node to `n`
> must go through `d`.

With that in mind, let's look at an example. This is contrived and fails
to compile, but it demonstrates the dominance check:

    define double @example(double %n) {
    entry:
      %x = fadd double %n, 1.00e+00;
      br label %loopB
    loop: ; Notice that loop is unreachable
      %i = phi double [ %a, %loop ]
      %a = fadd double %x, %i
      br label %loop
    loopB:
      %foobar = fcmp ult double %i, %n
      br label %afterloop
    afterloop:
      ret double 0.000000e+00
    }

Assembling this with `llc` fails with:

    Instruction does not dominate all uses!
      %i = phi double [ %a, %loop ]
      %foobar = fcmp ult double %i, %n

What's the problem? In `loopB`, we refer to `%i`, but `%i` is declared in
`loop`, and `loop` does not dominate `loopB`. That is, there exists a path
from `entry` to `loopB` that does not go through `loop`. Intuitively, it
makes sense that this program is not well formed. There is a path (indeed,
the only path) to the `%foobar = fcmp ult double %i, %n` instruction where
`%i` is not assigned. Consequently, the program does not make sense.

Notice however, that `%x` *can* be used anywhere in the function. This is
because `entry` dominates all the other blocks. In fact, even if other
blocks jumped back to `entry`, `%x` would still be available everywhere
because `entry` would be consider a *header block* for a loop. These are
discussed on the [Loop Terminology](https://llvm.org/docs/LoopTerminology.html)
page in the manual.

Actually, that's not true. You are not allows to jump back to the entry
block of a function. But here's a well-formed (again, contrived) program
that is accepted:

    define i32 @example(i32 %n) {
    entry:
      br label %beta
    beta:
      %x = add i32 %n, 1
      %condLoopA = icmp slt i32 %x, 0
      br i1 %condLoopA, label %gamma, label %beta
    gamma:
      %condLoopB = icmp slt i32 %x, 0
      br i1 %condLoopB, label %beta, label %gamma
    done:
      ret i32 42
    }

Notice that `beta` and `gamma` form a loop where `beta` is the header block.
It dominates `gamma`. Consequently, `%x` can be used in `gamma`. If you
move the definition of `%x` to the beginning of `gamma`, the program is
instead rejected.
