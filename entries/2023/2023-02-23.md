# Searching for Magic Constaints with an SMT Solver

The other day, I way looking for a magic constant M with this property:

    // Binary syntax with variables bound to bits. The left argument
    // to the multipy operator is a 4-bit number.
    abcd * M = ????_???a_????_???b_????_???c_????_???d

so that, with the expression `(x * M) & 0x01010101`, we could spread out
bits by putting 7 zeros between each of them. One way to find this constant
is with an SMT solver:

    (set-logic QF_BV)
    (set-option :produce-models true)
    (declare-const multiplier (_ BitVec 32))
    (define-fun applyMultiplier ((arg (_ BitVec 4))) (_ BitVec 32) (bvmul (concat #x0000000 arg) multiplier))
    (define-fun makeConstraints ((arg (_ BitVec 4))) Bool
      ( and
        (= ((_ extract 0 0) arg) ((_ extract 0 0) (applyMultiplier arg)))
        (= ((_ extract 1 1) arg) ((_ extract 8 8) (applyMultiplier arg)))
        (= ((_ extract 2 2) arg) ((_ extract 16 16) (applyMultiplier arg)))
        (= ((_ extract 3 3) arg) ((_ extract 24 24) (applyMultiplier arg)))
      )
    )
    (assert
      ( and
        ( makeConstraints #b0000 )
        ( makeConstraints #b0001 )
        ( makeConstraints #b0010 )
        ( makeConstraints #b0011 )
        ( makeConstraints #b0100 )
        ( makeConstraints #b0101 )
        ( makeConstraints #b0110 )
        ( makeConstraints #b0111 )
        ( makeConstraints #b1000 )
        ( makeConstraints #b1001 )
        ( makeConstraints #b1010 )
        ( makeConstraints #b1011 )
        ( makeConstraints #b1100 )
        ( makeConstraints #b1101 )
        ( makeConstraints #b1110 )
        ( makeConstraints #b1111 )
      )
    )
    (check-sat)
    (get-model)

If you feed this to z3, it spits out a solution: 0x00204085. The fact that a solver
can figure this out is not that impressive though. After all, it's simply to write
a C program to just brute force all 4 billion possible values for M (multiplying
each by 16 different constants). Or even fewer since the high 7 bits of M do not
influence the outcome (so 2^25 values to check for M, or about 32 million values).
What's more impressive is that we can adapt this to look for 64-bit words and that
z3 can tell us that there is no solution at all. Here's the new problem in a
symbolic form:

    abcdefgh * M = ????_???a_????_???b_????_???c_????_???d_????_???e_????_???f_????_???g_????_???h

And here is an stmlib file to that attempts to solve for M:

    (set-logic QF_BV)
    (declare-const multiplier (_ BitVec 64))
    (define-fun applyMultiplier ((arg (_ BitVec 8))) (_ BitVec 64) (bvmul (concat #x00000000000000 arg) multiplier))
    (define-fun makeConstraints ((arg (_ BitVec 8))) Bool
      ( and
        (= ((_ extract 0 0) arg) ((_ extract 0 0) (applyMultiplier arg)))
        (= ((_ extract 1 1) arg) ((_ extract 8 8) (applyMultiplier arg)))
        (= ((_ extract 2 2) arg) ((_ extract 16 16) (applyMultiplier arg)))
        (= ((_ extract 3 3) arg) ((_ extract 24 24) (applyMultiplier arg)))
        (= ((_ extract 4 4) arg) ((_ extract 32 32) (applyMultiplier arg)))
        (= ((_ extract 5 5) arg) ((_ extract 40 40) (applyMultiplier arg)))
        (= ((_ extract 6 6) arg) ((_ extract 48 48) (applyMultiplier arg)))
        (= ((_ extract 7 7) arg) ((_ extract 56 56) (applyMultiplier arg)))
      )
    )
    (define-fun makeConstraintsHigh ((arg (_ BitVec 4))) Bool
      ( and
        (makeConstraints (concat #b0000 arg))
        (makeConstraints (concat #b0001 arg))
        (makeConstraints (concat #b0010 arg))
        (makeConstraints (concat #b0011 arg))
        (makeConstraints (concat #b0100 arg))
        (makeConstraints (concat #b0101 arg))
        (makeConstraints (concat #b0110 arg))
        (makeConstraints (concat #b0111 arg))
        (makeConstraints (concat #b1000 arg))
        (makeConstraints (concat #b1001 arg))
        (makeConstraints (concat #b1010 arg))
        (makeConstraints (concat #b1011 arg))
        (makeConstraints (concat #b1100 arg))
        (makeConstraints (concat #b1101 arg))
        (makeConstraints (concat #b1110 arg))
        (makeConstraints (concat #b1111 arg))
      )
    )
    (assert
      ( and
        ( makeConstraintsHigh #b0000 )
        ( makeConstraintsHigh #b0001 )
        ( makeConstraintsHigh #b0010 )
        ( makeConstraintsHigh #b0011 )
        ( makeConstraintsHigh #b0100 )
        ( makeConstraintsHigh #b0101 )
        ( makeConstraintsHigh #b0110 )
        ( makeConstraintsHigh #b0111 )
        ( makeConstraintsHigh #b1000 )
        ( makeConstraintsHigh #b1001 )
        ( makeConstraintsHigh #b1010 )
        ( makeConstraintsHigh #b1011 )
        ( makeConstraintsHigh #b1100 )
        ( makeConstraintsHigh #b1101 )
        ( makeConstraintsHigh #b1110 )
        ( makeConstraintsHigh #b1111 )
      )
    )
    (check-sat)

I have omitted `produce-models` and `get-model` since there is no solution.
This time, z3 finds that there is no solution in about 1 second. As before,
the high 7 bits of any possible value for M do not matter, but that leaves
57 bits that could matter. Since enumerating 2^57 values would take much
too long, z3 is now doing something that we could not do with a naive
C program.

# Using Machine Instructions Instead

Those familiar with x86's many extensions will recognize that the `PDEP`
instruction does exactly this kind of "bit-spreading" operation. (I made
that word up.) Still, for the 32-bit number, doing the `MUL` and `AND`
might outperform `PDEP` since `PDEP` is a rather slow instruction. I've
not benchmarked this though, and it's just a guess.
