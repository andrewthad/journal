# Strongly Typed AST with User-Defined Data Types

Classic examples of strongly typed ASTs do not admit user-defined data types.
For example, Eisenberg's [Stitch](https://dl.acm.org/doi/pdf/10.1145/3406088.3409015)
uses an expression type that looks like this:

    data Exp :: forall n. Ctx n -> Type -> Type where
      Var   :: Elem ctx ty -> Exp ctx ty
      Lam   :: TypeRep arg -> Exp (arg :> ctx) res -> Exp ctx (arg -> res)
      App   :: Exp ctx (arg -> res) -> Exp ctx arg -> Exp ctx res
      Let   :: Exp ctx rhs_ty -> Exp (rhs_ty :> ctx) body_ty -> Exp ctx body_ty
      Arith :: Exp ctx Int -> ArithOp ty -> Exp ctx Int -> Exp ctx ty
      Cond  :: Exp ctx Bool -> Exp ctx ty -> Exp ctx ty -> Exp ctx ty
      Fix   :: Exp ctx (ty -> ty) -> Exp ctx ty
      IntE  :: Int -> Exp ctx Int
      BoolE :: Bool -> Exp ctx Bool

This ensures, by construction, that there are no references to unbound
variables. However, it cannot be used for a language in which nominal data
types are part of the program. Adding support for this difficult. Here are
the goals:

* Support for type constructors
* Strongly typed `Value`s guaranteed to have all fields
* Strongly typed projection
* Admit data constructors as types. That is, a value of type `Cons`
  should have `head` and `tail` fields available, but a value of
  type `List` must be scrutinized. This is not how Haskell does this,
  but it is how KindLang works, and I think this is an important feature.
* Allow supertypes (e.g. `List`) to have some fields projected if they
  live at the supertype level.

Here is what I have come up with so far:

    data Blueprint :: GHC.Type where
      Parent :: forall (n :: Nat). Vec n Blueprint -> Blueprint
      Leaf :: Blueprint
    
    data Con :: Blueprint -> GHC.Type where
      Terminal :: forall (b :: Blueprint). Con b
      Subtype :: forall (n :: Nat) (v :: Vec n Blueprint) (fin :: Fin n).
        SingFin fin -> Con ('Parent v) -> Con (Lookup fin v)
    
    type ListBlueprint = 'Parent @Two ('VCons 'Leaf ('VCons 'Leaf 'VNil))
    type ListTy = 'Terminal @ListBlueprint
    type NilTy = 'Subtype 'SFZ ListTy
    type ConsTy = 'Subtype ('SFS 'SFZ) ListTy

The way this works is that a type (called `Con` in the code) is a finger into
a blueprint. The blueprint tells up what all possible types in the universe
are. Above, `ListBlueprint` defines a blueprint with three types: `Nil`,
`Cons`, and a supertype `List`. As the finger goes deeper into the blueprint,
the blueprint is shorn away. `Nil` and `Cons` both have a blueprint of `Leaf`
because you cannot go any further into them.

Why is this design necessary? One important constraint is that, when we have
a value of type `List`, we need to be able to use existential quantification
to say that there is something inside that we don't know about. But we must
constrain what is inside. The blueprint gives us that power. Here is what
that looks like:

    -- Note: Atom is not truly general.
    data Atom :: forall (b :: Blueprint). Con b -> GHC.Type where
      AtomCons :: Int -> Value ListTy -> Atom ConsTy
      AtomNil :: Atom NilTy
      AtomList :: Atom ListTy

    data GroundValue :: forall (b :: Blueprint). Con b -> GHC.Type where
      GroundValue :: Atom con -> GroundValueNext con -> GroundValue con
    
    data GroundValueNext :: forall (b :: Blueprint). Con b -> GHC.Type where
      GroundValueNextSubtype :: GroundValue con -> GroundValueNext ('Subtype sfin con)
      GroundValueNextTerminal :: GroundValueNext 'Terminal
    
    data Value :: forall (b :: Blueprint). Con b -> GHC.Type where
      Data :: GroundValue con -> Value @'Leaf con
      Hide :: forall (n :: Nat) (v :: Vec n Blueprint) (con :: Con ('Parent v)) (f :: Fin n) (c :: Con (Lookup f v)).
        SingFin f -> Value @(Lookup f v) c -> Value @('Parent v) con

A value of some type will drill through any supertypes until it hits a
ground type. Then, `GroundValue` moves back up in the opposite direction to
include any fields that belong to supertypes.
