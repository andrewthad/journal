# Grammars

Grammophone is a neat tool that tells you whether or not you can use
various parsing algorithms to recognize a grammar (LL(1), LR(0), LR(1),
SLR(1), LALR(1)). It can be used in a browser and is available
[online](https://mdaines.github.io/grammophone). Similar tools
are available:

* The [tool](https://smlweb.cpsc.ucalgary.ca/start.html) at Univerity
  of Calgary lets you use multi-character symbols.

The lua reference manual [provides a BNF](https://www.lua.org/manual/5.1/manual.html).
It has a warning that operator precedence is not captured by the grammar,
suggesting that the user apply the shunting yard algorithm as a postprocessing
step. This extended BNF uses curly braces for repetition and square braces
for optionality (0 or 1 occurrences). The grammar looks like this:

    chunk ::= {stat [`;´]} [laststat [`;´]]
    block ::= chunk
    stat ::=  varlist `=´ explist | 
              functioncall | 
              do block end | 
              while exp do block end | 
              repeat block until exp | 
              if exp then block {elseif exp then block} [else block] end | 
              for Name `=´ exp `,´ exp [`,´ exp] do block end | 
              for namelist in explist do block end | 
              function funcname funcbody | 
              local function Name funcbody | 
              local namelist [`=´ explist] 
    laststat ::= return [explist] | break
    funcname ::= Name {`.´ Name} [`:´ Name]
    varlist ::= var {`,´ var}
    var ::=  Name | prefixexp `[´ exp `]´ | prefixexp `.´ Name 
    namelist ::= Name {`,´ Name}
    explist ::= {exp `,´} exp
    exp ::=  nil | false | true | Number | String | `...´ | function | 
             prefixexp | tableconstructor | exp binop exp | unop exp 
    prefixexp ::= var | functioncall | `(´ exp `)´
    functioncall ::=  prefixexp args | prefixexp `:´ Name args 
    args ::=  `(´ [explist] `)´ | tableconstructor | String 
    function ::= function funcbody
    funcbody ::= `(´ [parlist] `)´ block end
    parlist ::= namelist [`,´ `...´] | `...´
    tableconstructor ::= `{´ [fieldlist] `}´
    fieldlist ::= field {fieldsep field} [fieldsep]
    field ::= `[´ exp `]´ `=´ exp | Name `=´ exp | exp
    fieldsep ::= `,´ | `;´
    binop ::= `+´ | `-´ | `*´ | `/´ | `^´ | `%´ | `..´ | 
              `<´ | `<=´ | `>´ | `>=´ | `==´ | `~=´ | 
              and | or
    unop ::= `-´ | not | `#´

I can take a subset of it and rename things so that the Calgary tool
accepts it:

    Stat -> Varlist1 equals Explist1 .
    Varlist1 -> Var VarlistRep .
    VarlistRep -> comma Var VarlistRep | .
    Var -> name .
    Explist1 -> ExplistRep Exp .
    ExplistRep -> Exp comma ExplistRep | .
    Exp -> nil | false | true .

And we get nice output including a message that informs us:

> The grammar is not LL(1) because:
>
> * ExplistRep is nullable with clashing first and follow sets.

For some bizarre reason, the BNF defines Varlist1 and Explist1 with
the repeated parts on different sides. If we change this, it might
improve things:

    Stat -> Varlist1 equals Explist1 .
    Varlist1 -> Var VarlistRep .
    VarlistRep -> comma Var VarlistRep | .
    Var -> name .
    Explist1 -> Exp ExplistRep .
    ExplistRep -> comma Exp ExplistRep | .
    Exp -> nil | false | true .

Now the Calgary tool tells us that the grammar is LL(1).

# Concluding Thoughts

## EBNF Variety

It is surpising that there are not better CLI tools for working with grammars.
The ability to take an off-the-shelf BNF and analyze it would be neat. BNF is
not really standardized, but there the several extensions are common, and for
larger grammars it should be possible to guess conventions with reasonable
confidence. Here are things that I've seen in different EBNFs:

* Repetition can be star or curly braces
* One-or-more repetition can be plus
* Some presentations use case to distinguish between terminals and
  nonterminals. Lua just has all symbols without productions be
  terminals.
* Tricks for escaping infix operators can vary. Lua uses smart quotes or
  something like it, which actually reads very nicely.

## Improving Lua's BNF

Lua's BNF does some things that improve human comprehension but are bad
for parser generators. For example: 

    explist ::= {exp `,´} exp
    args ::=  `(´ [explist] `)´ | tableconstructor | String 

The symbol `explist` is defined to be nonempty sequence of comma-separated
expressions. But then in `args`, it is made optional. With a builtin "sep by"
supporting both 0-or-more and 1-or-more, we could have:

    explist ::= sepBy(+)(`,´)(exp)
    args    ::= `(´ sepBy(*)(`,´)(exp) `)´ | tableconstructor | String

## Grammar Equivalence Checking

Equality of CFGs is undecidable in general, but it would sure be nice
if there was some kind of procedure to help you figure out if a transformation
to a grammar caused it to accept different strings.

## Infix Operators

A useful general-purpose tool needs to be able to deal with operator
precedence. For example, for lua, no one would want to use a parser that
spat out the result from the BNF that ignored operator precedence.
I suppose that it makes the most sense to just manually apply the
transformation to the grammar to correct it.

# Trees of Tokens Instead of Arrays

I've thought about this before, but I wanted to revisit the idea. If you
preprocess the tokens in advantage to match up all of the pairs of parentheses,
you end up with a tree of tokens. It should be possible to parse this with
something like a CFG. I've never been able to find anything where anyone has
worked through the theory on this though.

One neat advantage of this approach is that it could eliminate the burden of
infinite lookahead and certain scenarios. Suppose that a languages included
two constructs:

    ( A ) -> X
    ( B ) => X

A and B are nonterminals that may be arbitrarily long, and the recognize some
of the same strings, but they are not equal. If we are parsing a tree instead
of a string, we no longer have to perform infinite lookahead to get to the
arrow that distinguishes the constructs. The parenthetical expression counts
as a single token, so an LL(2) parser could handle it.

Although this is a neat trick, I don't think the need for this is common, and
I doubt it's worth exploring more.

# The Grammar of C Sharp (C#)

There is a great answer by Eric Lippert to
[this StackOverflow question](https://stackoverflow.com/questions/16928725/is-cs-lambda-expression-grammar-lalr1)
that explains that C#'s grammar is ambiguous. I've saved this answer in
my library/resources directory as `c-sharp-grammar-ambiguous.md`. The two
critical ambiguous constructs are:

1. The combination of generics and function application. For example: `A ( B < C, D > ( E ) )`
2. Casting

# Lua's Grammar is Ambiguous

Turns out that it's right there in the manual. Lua 5.4's manual, in
section 3.3.1, explains:

> Both function calls and assignments can start with an open parenthesis.
> This possibility leads to an ambiguity in Lua's grammar. Consider the
> following fragment:
> 
>     a = b + c
>     (print or io.write)('done')
> 
> The grammar could see this fragment in two ways:
> 
>     a = b + c(print or io.write)('done')
>     a = b + c; (print or io.write)('done')
> 
> The current parser always sees such constructions in the first way,
> interpreting the open parenthesis as the start of the arguments to a call.
> To avoid this ambiguity, it is a good practice to always precede with
> a semicolon statements that start with a parenthesis:
> 
>     ;(print or io.write)('done')

Darn.
