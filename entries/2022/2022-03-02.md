# Low-Performance Loops in GHC

Here is a simple C function that adds up ten elements in an array:

    int64_t example(const int64_t* restrict xs) {
      int64_t acc = 0;
      for(int i = 0; i < 10; i++) {
        acc = acc + (xs[i] * 2);
      }
      return acc;
    }

With `gcc -masm=intel -fno-stack-protector -c -O2 -S input.c` (version 9.3.0),
we get this output (annotations added by me):

    example:                        // address xs is in rdi to begin
    .LFB0:
        .cfi_startproc
        endbr64
        lea    rcx, 80[rdi]         // max address is 80 + xs, rcx backs this
        xor    eax, eax             // initialize i to 0, eax backs acc
        .p2align 4,,10
        .p2align 3
    .L2:
        mov    rdx, QWORD PTR [rdi] // load xs[i] into rdx
        add    rdi, 8               // add 8 to address
        lea    rax, [rax+rdx*2]     // acc = acc + (element * 2)
        cmp    rdi, rcx             // test if the address has reached the end
        jne    .L2
        ret                         // acc is in eax, so no reg shuffling before ret
        .cfi_endproc

The body of the loop is just 5 instructions. Here is the same program
written in GHC Haskell:

    {-# language MagicHash #-}
    module Input (example) where
    import GHC.Exts
    example :: ByteArray# -> Int#
    example b =
      let go acc ix = case ix <# 10# of
            0# -> acc
            _ -> go (acc +# (indexIntArray# b ix *# 2#)) (ix +# 1#)
       in go 0# 0#

And here is the assembly output (NCG, not LLVM), and also, sorry, I could not
figure out how to get GHC to dump ASM with Intel syntax:

    Input.example_info:
    _cCZ:
    _cD1:
        xorl %eax,%eax
        xorl %ebx,%ebx
    _cD2:
        cmpq $10,%rax    // Major Problem
        setl %cl         // Major Problem
        movzbl %cl,%ecx  // Major Problem
        testq %rcx,%rcx
        je _cDa
    _cD9:
        movq 16(%r14,%rax,8),%rcx // The offset of 16 is because of the ByteArray header
        incq %rax
        shlq $1,%rcx     // Minor Problem
        addq %rcx,%rbx   // Minor Problem
        jmp _cD2
    _cDa:
        jmp *(%rbp)
        .size Input.example_info, .-Input.example_info

GHC's output has three important differences. I've categorized them
as major, minor, and micro.

* Major. Rather than perform `cmp` (or `cmpq`) followed by
  a conditional jump, GHC creates a sequence of instructions that copies the
  result in the flag register to `%cl`, copies that into `%ecx`, and then
  performs `testq`.
* Minor. GCC is rather clever with fusing multiplication and addition into
  a single `lea` instruction. GHC does not figure this out and performs
  `shlq` followed by `addq`.
* Micro. This isn't labeled, but GCC places the `i < 10` check at the
  end of the instruction sequences. GHC, by contrast, places it at the
  beginning. It's always false on the first iteration (`i` starts as 0),
  so it's fine to this.
