# Raising Exceptions in Other Threads

I came across an old Julia Evans blog post today,
[Why Ruby's Timeout is Dangerous](https://jvns.ca/blog/2015/11/27/why-rubys-timeout-is-dangerous-and-thread-dot-raise-is-terrifying/).
Her argument is that raising an exception in another thread is fraught with error.
Here's an excerpt that describes the problem:

> Nobody writes code to defend against an exception being raised on
> literally any line. That's not even possible. So `Thread.raise`
> is basically like a sneak attack on your code that could result
> in almost anything.

She discusses this in the context of Ruby, which I haven't touched in years.
However, the message rings true to my ears.
I mostly use GHC Haskell.
In this post, I would like to contribute three things to this discussion:

1. An explanation how GHC attacks this problem
2. Why GHC's approach is good
3. Why GHC's approach is painful

# How GHC Attacks This Problem

GHC explains it's process for dealing with this issue in the documentation for
[Control.Exception](https://hackage.haskell.org/package/base-4.16.1.0/docs/Control-Exception.html).
A simplified explanation (footnote 1) is that there two modes that a running program can be in: unmasked exceptions and masked exceptions.
Unmasked exceptions is the mode that a program is typically in.
It means that exception (one sent by another thread) could arrive at any point in time.
This is the "sneak attack" that Julia describes.
The other mode, masked exceptions, gives the user a way to defer the arrival of exceptions.
There are several ways to cause a program to transition to masked-exceptions mode.
The `ghc-prim` library offers `catch#`, `maskAsyncExceptions#`, and `unmaskAsyncExceptions#`.
`Control.Exception` builds a sophisticated offering on top of these.
Broadly, it provides these functions (all using just those three primitives):

* `finally`: Always execute cleanup after an instruction sequence (Ruby calls this `ensure`)
* `handle`: Execute cleanup after an instruction sequence if that sequence is interrupted by a specific exception (Ruby calls this `rescue`)
* `onException`: Execute cleanup after an instruction sequence if that sequence is interrupted by any exception (Ruby calls this `rescue`)
* `bracket`: Acquire a resource, use it, and release it.
  Acquisition cannot be interrupted by an exception.
  Use may be interrupted by an exception.
  Release happens regardless of whether or not an exception occurs.
  Release may not be interrupted by an exception.
  This is used to implement `System.IO.withFile`, which opens a file, uses it, and guarantees that it is closed.
  (Ruby lacks an equivalent construct)
* `mask`: Even more flexible than `bracket`.

# Why GHC's Approach Is Good

GHC is in a better situation than Ruby because it is possible to implement
functions like `withFile`, `withSocket`, `withMVar`, and `modifyMVar` safely.
These are all available in standard (or de-facto standard) libraries.

# Why GHC's Approach Is Painful

One problem is that, even with the machinery to do this, it's still hard for
users to remembers to use it. It requires an unnatural kind of defensiveness.
The other problem is that acquisition of multiple resources, where any acquisition may fail, is difficult.
Consider opening two files:

    openFile :: FilePath -> IOMode -> IO Handle
    openFile = ...

    closeFile :: Handle -> IO ()
    closeFile = ...

    -- This is not the real implementation of withFile, but it illustrates
    -- how bracket is used.
    withFile :: FilePath -> IOMode -> (Handle -> IO a) -> IO a
    withFile path mode callback = bracket
      (openFile path mode)
      closeFile
      callback

    useTwoFiles :: IO ()
    useTwoFiles = withFile "foo.txt" ReadMode $ \h1 -> withFile "bar.txt" ReadMode $ \h2 -> do
      consumeMyHandles h1 h2

Keep in mind, `withFile` is not actually implemented this way.
The real implementation is complicated by augmentation of exceptions (it adds something kind of like a stack trace) and by setting up a finalizer.
Setting the real-world complication aside, our simple implementation has two shortcomings:

1. Performance: This calls `bracket` twice, which adjusts the masking mode more times than is needed.
2. Exception Handling: If opening either file fails, this raises an exception.

To tackle either of these, we need a variant of `openFile` that returns
exceptions rather than throwing them.

    openFileE :: FilePath -> IOMode -> IO (Either IOException Handle)
    openFileE = ...

Let's resolve both issues (this uses `LambdaCase` and `TypeApplications` extensions for clarity):

    useTwoFiles :: IO (Either IOException ())
    useTwoFiles = bracket
      @(Either IOException (Handle,Handle)) -- instantiate a
      @() -- instantiate b
      @(Either IOException ()) -- instantiate c
      ( openFileE "foo.txt" ReadMode >>= \case
          Left e -> pure (Left e)
          Right h1 -> openFileE "bar.txt" ReadMode >>= \case
            Left e -> pure (Left e)
            Right h2 -> pure (Right (h1,h2))
      )
      (\r -> case r of
        Left _ -> pure ()
        Right (h1,h2) -> do
          closeFile h1
          closeFile h2
      )
      (\r -> case r of
        Left e -> pure (Left e)
        Right (h1,h2) -> do
          consumeMyHandles h1 h2
          pure (Right ())
      )

Threading the `Either` through to the two callbacks like this feels clumsy,
but it does at least work correctly.
One caveat is that this implementation still doesn't let `closeFile` return exceptions in a pure way.
Also, if an exception is throw while closing `h1`, then `h2` doesn't get closed. 
The problem with `h2` not getting closed could be fixed by making `closeFile` return an `Either`,
but that still doesn't give us a way to actually return that `Either` as the
result of `useTwoFiles`. To fix this, we need to rewrite `bracket`:

    -- The bracket defined in Control.Exception
    bracket
      :: IO a         -- ^ computation to run first ("acquire resource")
      -> (a -> IO b)  -- ^ computation to run last ("release resource")
      -> (a -> IO c)  -- ^ computation to run in-between
      -> IO c         -- returns the value from the in-between computation
    bracket before after thing =
      mask $ \restore -> do
        a <- before
        r <- restore (thing a) `onException` after a
        _ <- after a
        return r

    -- Tweaked bracket that can return the "release resource" result
    myBracket
      :: IO a                     -- ^ computation to run first ("acquire resource")
      -> (a -> IO (Either e ()))  -- ^ computation to run last ("release resource")
      -> (a -> IO c)              -- ^ computation to run in-between
      -> IO (Either e c)          -- returns the value from the in-between computation
    myBracket before after thing =
      mask $ \restore -> do
        a <- before
        r <- restore (thing a) `onException` after a
        after a >>= \case
          Left e -> pure (Left e)
          Right () -> pure (Right r)

I've just tweaked bracket to make it be what is needed to fix the example, but there are
other direction to go as well.
For example, the in-between computation could return an `Either` or the "acquire resource"
could return an `Either`.
It's possible to write these other variants.
And it's possible to use `mask` directly instead of writing any of these `bracket` variants.
Here's the final iteration of this program:

    closeFileE :: Handle -> IO (Either IOException ())
    closeFileE = ...

    useTwoFiles :: IO (Either IOException ())
    useTwoFiles = mask $ \restore -> do
      openFileE "foo.txt" ReadMode >>= \case
        Left e -> pure (Left e)
        Right h1 -> openFileE "bar.txt" ReadMode >>= \case
          Left e -> do
            _ <- closeFileE h1 -- throws any exception away, already have an exception
            pure (Left e)
          Right h2 -> do
            let cleanup :: IO (Either IOException ())
                cleanup = closeFileE h1 >>= \case
                  Left e -> 
                    _ <- closeFileE h2 -- throws any exception away, already have an exception
                    pure (Left e)
                  Right () -> closeFileE h2
            r <- restore (consumeMyHandles h1 h2) `onException` cleanup
            cleanup >>= \case
              Left e -> pure (Left e)
              Right () -> pure (Right r)

# Conclusion

I've illustrated how to use GHC's machinery. Beauty (or lack of it) is in the
eye of the beholder, and some readers might see that example and think
"that's not that bad". There are two sources of this complication:

1. Returning exceptions (use standard control flow) rather than throwing them
2. Allow other threads to throw an exception to our thread ("sneak attack")

If we drop goal (1), we are back to not figthing against the grain:

    useTwoFiles :: IO ()
    useTwoFiles = withFile "foo.txt" ReadMode $ \h1 -> withFile "bar.txt" ReadMode $ \h2 -> do
      consumeMyHandles h1 h2

If we drop goal (2) and if we eliminate exceptional control flow from the language, we get:

    useTwoFiles :: IO (Either IOException ())
    useTwoFiles = do
      openFileE "foo.txt" ReadMode >>= \case
        Left e -> pure (Left e)
        Right h1 -> openFileE "bar.txt" ReadMode >>= \case
          Left e -> do
            _ <- closeFileE h1 -- throws any exception away, already have an exception
            pure (Left e)
          Right h2 -> do
            r <- consumeMyHandles h1 h2
            closeFileE h1 >>= \case
              Left e -> 
                _ <- closeFileE h2 -- throws any exception away, already have an exception
                pure (Left e)
              Right () -> closeFileE h2

I prefer this last variant, although I have a hard time articulating why.

(1) The masked-exception mode is actually split into two other modes:
    interruptible and uninterruptible. Interruptible mode allows certain
    blocking operations to be interrupted, and uninterruptible mode
    truly prevents the delivery of all exceptions. The documentation
    in `Control.Exception` explains this, but it does not describe any
    situations where uninterruptible mode is desirable.
